

# xg-risk-rmq-1

## 时间线

- 12:03:15 开始，连接建立速率开始增加，初始为 10+ 连接/秒
- 12:04:00 左右，连接建立速率达到最高峰 100+ 连接/秒
- 12:04:20 左右，连接建立速率降回 10+ 连接/秒
- 12:05:22 出现 1 次 {badmatch,{error,{channel_operation_timeout,5000}}}
- 12:05:41 出现 2 次 {badmatch,{error,{channel_operation_timeout,5000}}}
- 12:05:43 出现 1 次 {badmatch,{error,{channel_operation_timeout,5000}}}
- 12:14:42 出现 4 次 killed
- 12:19:05 出现 1 次 killed
- 12:19:06 出现 7 次 killed
- 12:20:20 出现 8 次 channel_termination_timeout

## 连接创建分布情况

在 12:05:22 时间点之前，能够看到的只有向 xg-risk-rmq-1 建立大量连接的情况；
此时日志中没有特别的异常；

大致连接波动情况如下：
- 存在两个峰值；
- 最高速率为 100+ 连接/s

## 错误日志解析

### {badmatch,{error,{channel_operation_timeout,5000}}}

在 sasl 日志中看到，从 12:05:22  开始出现 `CRASH REPORT` ；

```shell
=CRASH REPORT==== 3-Sep-2016::12:05:22 ===
  crasher:
    initial call: gen:init_it/6
    pid: <0.18881.6190>            %% 发生 crash 进程的 pid
    registered_name: []
    exception exit: {{badmatch,{error,{channel_operation_timeout,5000}}},
                     [{rabbit_channel,terminate,2,
                                      [{file,"src/rabbit_channel.erl"},
                                       {line,614}]},
                      {gen_server2,terminate,3,
                                   [{file,"src/gen_server2.erl"},{line,1146}]},
                      {proc_lib,wake_up,3,
                                [{file,"proc_lib.erl"},{line,249}]}]}
      in function  gen_server2:terminate/3 (src/gen_server2.erl, line 1149)
    ancestors: [<0.3384.6141>,<0.6490.6193>,<0.8898.6179>,<0.12511.7656>,
                  <0.25522.2>,<0.25521.2>,<0.25520.2>,rabbit_sup,<0.25348.2>]
    messages: []
    links: []
    dictionary: [{{queue_stats,
                       {resource,<<"risk">>,queue,<<"asynctomongo">>}},
                   [{ack,1912117},{deliver,1912117},{redeliver,28}]},
                  {guid_secure,
                      {{2,'rabbit@xg-risk-rmq-1',#Ref<0.0.297033.218495>},0}},
                  {channel_operation_timeout,5000},
                  {msg_size_for_gc,257154},
                  {process_name,
                      {rabbit_channel,
                          {<<"10.0.29.66:20231 -> 10.0.22.20:5672">>,1}}}]
    trap_exit: true
    status: running
    heap_size: 1598
    stack_size: 27
    reductions: 235923352
  neighbours:

=SUPERVISOR REPORT==== 3-Sep-2016::12:05:22 ===
     Supervisor: {<0.3384.6141>,rabbit_channel_sup}
     Context:    shutdown_error
     Reason:     {{badmatch,{error,{channel_operation_timeout,5000}}},
                  [{rabbit_channel,terminate,2,
                                   [{file,"src/rabbit_channel.erl"},
                                    {line,614}]},
                   {gen_server2,terminate,3,
                                [{file,"src/gen_server2.erl"},{line,1146}]},
                   {proc_lib,wake_up,3,[{file,"proc_lib.erl"},{line,249}]}]}
     Offender:   [{pid,<0.18881.6190>},      %% 发生 crash 进程的 pid
                  {name,channel},
                  {mfargs,
                      {rabbit_channel,start_link,
                          [1,<0.8093.6184>,<0.19131.6174>,<0.8093.6184>,
                           <<"10.0.29.66:20231 -> 10.0.22.20:5672">>,
                           rabbit_framing_amqp_0_9_1,
                           {user,<<"risk">>,
                               [monitoring],
                               [{rabbit_auth_backend_internal,none}]},
                           <<"risk">>,
                           [{<<"exchange_exchange_bindings">>,bool,true},
                            {<<"connection.blocked">>,bool,true},
                            {<<"authentication_failure_close">>,bool,true},
                            {<<"basic.nack">>,bool,true},
                            {<<"publisher_confirms">>,bool,true},
                            {<<"consumer_cancel_notify">>,bool,true}],
                           <0.2544.148>,<0.24394.5787>]}},
                  {restart_type,intrinsic},
                  {shutdown,70000},
                  {child_type,worker}]
```

对应 log 日志中的内容如下

```shell
=ERROR REPORT==== 3-Sep-2016::12:05:22 ===
** Generic server <0.18881.6190> terminating

%% 因为 <0.3384.6141> 对应 rabbit_channel_sup 进程
%% 因此下面信息表明了：当前 channel 进程终止的原因是由于 rabbit_channel_sup 发送了 {'EXIT',<0.3384.6141>,shutdown} 消息；

** Last message in was {'EXIT',<0.3384.6141>,shutdown}
** When Server state == {ch,running,rabbit_framing_amqp_0_9_1,1,
                         <0.8093.6184>,<0.19131.6174>,<0.8093.6184>,
                         <<"10.0.29.66:20231 -> 10.0.22.20:5672">>,
                         {lstate,<0.24394.5787>,false},
                         none,1912118,
                         {[],[]},
                         {user,<<"risk">>,
                          [monitoring],
                          [{rabbit_auth_backend_internal,none}]},
                         <<"risk">>,<<"asynctomongo">>,
                         {dict,1,16,16,8,80,48,
                          {[],[],[],[],[],[],[],[],[],[],[],[],[],[],[],[]},
                          {{[],[],[],[],[],[],[],[],[],[],[],[],[],[],
                            [[<0.19961.156>|
                              {resource,<<"risk">>,queue,<<"asynctomongo">>}]],
                            []}}},
                         {state,
                          {dict,1,16,16,8,80,48,
                           {[],[],[],[],[],[],[],[],[],[],[],[],[],[],[],[]},
                           {{[],[],[],[],[],[],[],[],[],[],[],[],[],[],
                             [[<0.19961.156>|#Ref<0.0.297033.218499>]],
                             []}}},
                          erlang},
                         {dict,1,16,16,8,80,48,
                          {[],[],[],[],[],[],[],[],[],[],[],[],[],[],[],[]},
                          {{[],[],[],[],[],[],[],[],[],[],[],
                            [[<<"amq.ctag-yVL9PsI6oqYtBhih3p2LiQ">>|
                              {{amqqueue,
                                {resource,<<"risk">>,queue,<<"asynctomongo">>},
                                false,false,none,[],<0.19961.156>,[],[],[],
                                undefined,[],[],live},
                               {false,16,false,[]}}]],
                            [],[],[],[]}}},
                         {dict,1,16,16,8,80,48,
                          {[],[],[],[],[],[],[],[],[],[],[],[],[],[],[],[]},
                          {{[],[],[],[],[],[],[],[],[],[],[],[],[],[],
                            [[<0.19961.156>|
                              {1,
                               {<<"amq.ctag-yVL9PsI6oqYtBhih3p2LiQ">>,nil,
                                nil}}]],
                            []}}},
                         {set,1,16,16,8,80,48,
                          {[],[],[],[],[],[],[],[],[],[],[],[],[],[],[],[]},
                          {{[],[],[],[],[],[],[],[],[],[],[],[],[],[],
                            [<0.19961.156>],
                            []}}},
                         <0.2544.148>,
                         {state,fine,5000,undefined},
                         false,1,
                         {{0,nil},{0,nil}},
                         [],
                         {{0,nil},{0,nil}},
                         [{<<"exchange_exchange_bindings">>,bool,true},
                          {<<"connection.blocked">>,bool,true},
                          {<<"authentication_failure_close">>,bool,true},
                          {<<"basic.nack">>,bool,true},
                          {<<"publisher_confirms">>,bool,true},
                          {<<"consumer_cancel_notify">>,bool,true}],
                         none,16,none,flow,[]}
** Reason for termination == 
** {{badmatch,{error,{channel_operation_timeout,5000}}},
    [{rabbit_channel,terminate,2,[{file,"src/rabbit_channel.erl"},{line,614}]},
     {gen_server2,terminate,3,[{file,"src/gen_server2.erl"},{line,1146}]},
     {proc_lib,wake_up,3,[{file,"proc_lib.erl"},{line,249}]}]}
** In 'terminate' callback with reason ==
** shutdown
```

#### 源码分析

在 `proc_lib.erl` 中

```erlang
wake_up(M, F, A) when is_atom(M), is_atom(F), is_list(A) ->
    try
	    %% 调用 gen_server2:terminate/3 的地方
        apply(M, F, A) 
    catch
        Class:Reason ->
            exit_p(Class, Reason)
    end.

%% 在以 Reason 原因退出前，先发出 crash 报告
exit_p(Class, Reason) ->
    case get('$initial_call') of
        {M,F,A} when is_atom(M), is_atom(F), is_integer(A) ->
            MFA = {M,F,make_dummy_args(A, [])},
            crash_report(Class, Reason, MFA),
            exit(Reason);
        _ ->
            %% The process dictionary has been cleared or
            %% possibly modified.
            crash_report(Class, Reason, []),
            exit(Reason)
    end.
...

%% -----------------------------------------------------
%% Generate a crash report.
%% -----------------------------------------------------

crash_report(exit, normal, _)       -> ok;
crash_report(exit, shutdown, _)     -> ok;
crash_report(exit, {shutdown,_}, _) -> ok;
%% 处理异常退出情况
crash_report(Class, Reason, StartF) ->
    OwnReport = my_info(Class, Reason, StartF),
    LinkReport = linked_info(self()),
    Rep = [OwnReport,LinkReport],
    %% 输出 crash report 的地方
    error_logger:error_report(crash_report, Rep).

```

在 `gen_server2.erl` 中

```erlang
%%% ---------------------------------------------------
%%% Terminate the server.
%%% ---------------------------------------------------

%% 对应上面调用 apply(M, F, A) 的地方
terminate(Reason, Msg, #gs2_state { name  = Name,
                                    mod   = Mod,
                                    state = State,
                                    debug = Debug }) ->
    %% 根据上下文，这里有
    %% Mod => rabbit_channel
    %% Reason => shutdown
    case catch Mod:terminate(Reason, State) of
        {'EXIT', R} ->  %% 在终止的过程中抛出异常
	        %% 输出 =ERROR REPORT==== 信息
            error_info(R, Reason, Name, Msg, State, Debug),
            exit(R);
        _ ->
            case Reason of
                normal ->
                    exit(normal);
                shutdown ->
                    exit(shutdown);
                {shutdown,_}=Shutdown ->
                    exit(Shutdown);
                _ ->
                    error_info(Reason, undefined, Name, Msg, State, Debug),
                    exit(Reason)
            end
    end.
```

在 `rabbit_channel.erl` 中

```erlang
terminate(Reason, State) ->
    %% 当前 channel 在终止时，需要通知与自身相关的所有 queue
    %% 对应的 queue 可能存在于 本地 和 远端 节点 

	%% 在当前上下文中，这里会的到
    %% Res => {error, {channel_operation_timeout, Timeout}}
    {Res, _State1} = notify_queues(State),
    %% 若 Reason 为 normal|shutdown|{shutdown,_Term} 中的一种，则 Res 必须为 ok ，否则触发 badmatch 异常
    case Reason of
        normal            -> ok = Res;
        shutdown          -> ok = Res;
        {shutdown, _Term} -> ok = Res;
        _                 -> ok
    end,
    pg_local:leave(rabbit_channels, self()),
    rabbit_event:if_enabled(State, #ch.stats_timer,
                            fun() -> emit_stats(State) end),
    rabbit_event:notify(channel_closed, [{pid, self()}]).

...

%% 向所有与当前 channel 有关的 queue 通知自己要挂了
notify_queues(State = #ch{state = closing}) ->
    {ok, State};
notify_queues(State = #ch{consumer_mapping  = Consumers,
                          delivering_queues = DQ }) ->
    %% 获取与当前 channel 相关的 queue 的 pid
    QPids = sets:to_list(
              sets:union(sets:from_list(consumer_queues(Consumers)), DQ)),
    %% 向所有与自己（当前 channel）有关的 queue 通知自己已经要挂了（closing）
    %% 根据上下文，这里得到了 {{error, {channel_operation_timeout, Timeout}}, State#ch{state = closing}} 这个结果
    {rabbit_amqqueue:notify_down_all(QPids, self(),
                                     get(channel_operation_timeout)),
     State#ch{state = closing}}.
```

在 `rabbit_amqqueue.erl` 中

```erlang
%% Timeout => 默认为 ?CHANNEL_OPERATION_TIMEOUT 即 5 秒超时；
notify_down_all(QPids, ChPid, Timeout) ->
	%% 基于 rpc 调用本地节点上的 delegate:call/2 再由 delegate 进程负责
	%% 将 channel 的 down 消息通知给所有相关的 queue 进程（本地节点＋远端节点）
    case rpc:call(node(), delegate, call,
                  [QPids, {notify_down, ChPid}], Timeout) of
        %% 在这里触发了超时异常！！
        {badrpc, timeout} -> {error, {channel_operation_timeout, Timeout}};
        {badrpc, Reason}  -> {error, Reason};
        {_, Bads} ->
            case lists:filter(
                   fun ({_Pid, {exit, {R, _}, _}}) ->
                           rabbit_misc:is_abnormal_exit(R);
                       ({_Pid, _})                 -> false
                   end, Bads) of
                []    -> ok;
                Bads1 -> {error, Bads1}
            end;
        Error         -> {error, Error}
    end.

```

从这里可以看出根本的原因是：在 5 秒时间内没有完成“将 channel 关闭信息通知给相关 queue“的通知工作，导致发生了 rpc 调用 timeout 异常；

#### 原因总结

虽然从代码分析中已经能够确认问题是由于 rpc 调用超时导致的异常，但这里仍旧有问题需要弄清楚：
- 多处崩溃信息的先后顺序是？
- 导致 rpc 调用超时的原因是什么？网络状况不好？超时参数设置的过小？

针对第一个问题，重新回顾一下上面的调用栈：
```shell
     Reason:  {{badmatch,{error,{channel_operation_timeout,5000}}},
               [{rabbit_channel,terminate,2,[{file,"src/rabbit_channel.erl"},{line,614}]},
                {gen_server2,terminate,3,[{file,"src/gen_server2.erl"},{line,1146}]},
                {proc_lib,wake_up,3,[{file,"proc_lib.erl"},{line,249}]}]}
```

我们可以采取反向推理的方式得出结论：
- 在通过 rpc 通知 channel 挂掉消息时发生了 timeout 超时；
- rpc 调用发生在 `rabbit_channel:terminate/2` 中，用于在当前 channel 终止时，通知与其相关的所有 queue ；并且这些 queue 既可能存在于本地节点，也可能存在于远端节点；
- `rabbit_channel:terminate/2` 调用发生在 `gen_server2:terminate/3` 中，并采用 catch 进行了异常捕获；由于成功捕获到 timeout 超时异常，在输出“=ERROR REPORT====“后，通过 `exit/1` 调用终止当前调用进程；
- `gen_server2:terminate/3` 是在 `proc_lib:wake_up/3` 中调用的，而该函数是在进程进入休眠状态时设置的回调；该回调的触发条件是在休眠状态下收到一条消息即可；
- 由上下文可知，rabbit_channel 进程收到的这条消息为 *{'EXIT',<0.3384.6141>,shutdown}* ，其中 <0.3384.6141> 为 rabbit_channel_sup 进程 pid ；


> ⚠️ 关于最后这条消息产生的原因，目前尚不清楚；根据经验，该消息应该表示 <0.3384.6141> 对应的进程以 shutdown 原因退出了；同时，鉴于 <0.3384.6141> 对应的是 rabbit_channel_sup 监督者进程，而该进程几乎可以认定不回出现崩溃等异常，因此怀疑是 rabbit_channel_sup 下管理的其他进程（rabbit_writer 或 rabbit_limiter）出现异常，进而导致 rabbit_channel_sup 进程退出的；

> ⚠️ 需要增加一个小实验进行验证；

----------

### killed

```shell
=SUPERVISOR REPORT==== 3-Sep-2016::12:14:42 ===
     Supervisor: {<0.674.1007>,rabbit_channel_sup}
     Context:    shutdown_error
     Reason:     killed
     Offender:   [{pid,<0.15076.752>},
                  {name,channel},
                  {mfargs,
                      {rabbit_channel,start_link,
                          [1,<0.28596.983>,<0.31690.1065>,<0.28596.983>,
                           <<"10.0.29.67:25776 -> 10.0.22.20:5672">>,
                           rabbit_framing_amqp_0_9_1,
                           {user,<<"risk">>,
                               [monitoring],
                               [{rabbit_auth_backend_internal,none}]},
                           <<"risk">>,
                           [{<<"exchange_exchange_bindings">>,bool,true},
                            {<<"connection.blocked">>,bool,true},
                            {<<"authentication_failure_close">>,bool,true},
                            {<<"basic.nack">>,bool,true},
                            {<<"publisher_confirms">>,bool,true},
                            {<<"consumer_cancel_notify">>,bool,true}],
                           <0.19319.983>,<0.8492.1054>]}},
                  {restart_type,intrinsic},
                  {shutdown,70000},
                  {child_type,worker}]
````

从上述信息中可以知道：channel 进程是被自己的监督者进程通过 kill 信号强杀的，但为何会被强杀，尚需要深入研究！


----------

### channel_termination_timeout

在 12:20:20 这个时刻，sasl 日志中出现 8 次如下类型的错误

```shell
=CRASH REPORT==== 3-Sep-2016::12:20:20 ===
  crasher:
    initial call: rabbit_reader:init/4
    pid: <0.4643.1139>
    registered_name: []
    exception exit: channel_termination_timeout
      in function  rabbit_reader:wait_for_channel_termination/3 (src/rabbit_reader.erl, line 771)
      in call from rabbit_reader:send_error_on_channel0_and_close/4 (src/rabbit_reader.erl, line 1511)
      in call from rabbit_reader:terminate/2 (src/rabbit_reader.erl, line 616)
      in call from rabbit_reader:handle_other/2 (src/rabbit_reader.erl, line 541)
      in call from rabbit_reader:mainloop/4 (src/rabbit_reader.erl, line 503)
      in call from rabbit_reader:run/1 (src/rabbit_reader.erl, line 428)
      in call from rabbit_reader:start_connection/4 (src/rabbit_reader.erl, line 386)
    ancestors: [<0.29481.1143>,<0.25522.2>,<0.25521.2>,<0.25520.2>,
                  rabbit_sup,<0.25348.2>]
    messages: [{'EXIT',#Port<0.228098766>,normal}]
    links: []
    dictionary: [{{channel,1},
                   {<0.1465.958>,{method,rabbit_framing_amqp_0_9_1}}},
                  {{ch_pid,<0.1465.958>},{1,#Ref<0.0.298622.127946>}},
                  {process_name,
                      {rabbit_reader,
                          <<"10.0.29.67:34834 -> 10.0.22.20:5672">>}}]
    trap_exit: true
    status: running
    heap_size: 2586
    stack_size: 27
    reductions: 6746
  neighbours:

=SUPERVISOR REPORT==== 3-Sep-2016::12:20:20 ===
     Supervisor: {<0.29481.1143>, rabbit_connection_sup}
     Context:    shutdown_error
     Reason:     channel_termination_timeout
     Offender:   [{pid,<0.4643.1139>},
                  {name,reader},
                  {mfargs,
                      {rabbit_reader,start_link,
                          [<0.22999.924>,
                           {acceptor,{0,0,0,0,0,0,0,0},5672},
                           #Port<0.228098766>]}},
                  {restart_type,intrinsic},
                  {shutdown,4294967295},
                  {child_type,worker}]
```

对应 log 日志中有

```shell
...
=INFO REPORT==== 3-Sep-2016::12:18:04 ===
accepting AMQP connection <0.4643.1139> (10.0.29.67:34834 -> 10.0.22.20:5672)
...
=ERROR REPORT==== 3-Sep-2016::12:20:14 ===
Error on AMQP connection <0.4643.1139> (10.0.29.67:34834 -> 10.0.22.20:5672, vhost: 'risk', user: 'risk', state: running), channel 0:
operation none caused a connection exception connection_forced: "broker forced connection closure with reason 'shutdown'"
```

按照时间线梳理后，结合原始日志内容，可以得到如下结论：
- 在 12:18:04 时刻，出现较多的连接建立（16 个 AMQP 连接）
- 在 12:20:14 时刻，由于某种原因 rabbit_connection_sup 以 shutdown 原因强行终止该 connection ；
- 之后 rabbit_reader 进程会收到 {'EXIT', Parent, shutdown} 消息，并将"=ERROR REPORT===="写入 log 日志；
- 接着执行先关闭 connection 下的所有 channel ，再关闭相应 connection 的动作；结果在关闭 channel 的过程中触发了 channel_termination_timeout 异常；
- 在 12:20:20 时刻，由于 handle_other/2 没有针对异常捕获，因此 connection 进程崩溃退出，最终在 sasl 中留下异常日志；

这里的遗留问题是：在限定时间内无法全部 channel 的情况是否属于正常情况？

> ⚠️ {shutdown,4294967295} 中的超大整数来自 `-define(MAX_WAIT, 16#ffffffff).` ，用于表示超长时间值；

相关代码如下

```erlang


%% 收到来自父进程 rabbit_connection_sup 的退出消息
handle_other({'EXIT', Parent, Reason}, State = #v1{parent = Parent}) ->
    terminate(io_lib:format("broker forced connection closure "
                            "with reason '~w'", [Reason]), State),
    maybe_emit_stats(State),
    %% 退出当前进程
    exit(Reason);

...

%% 在运行状态中被终止运行（例如收到父进程的退出消息）
terminate(Explanation, State) when ?IS_RUNNING(State) ->
    {normal, handle_exception(State, 0,
                              rabbit_misc:amqp_error(
                                connection_forced, Explanation, [], none))};
...

handle_exception(State = #v1{connection = #connection{protocol = Protocol},
                             connection_state = CS},
                 Channel, Reason)
  when ?IS_RUNNING(State) orelse CS =:= closing ->
    respond_and_close(State, Channel, Protocol, Reason, Reason);

...

respond_and_close(State, Channel, Protocol, Reason, LogErr) ->
    %% 在 log 中输出错误日志
    log_hard_error(State, Channel, LogErr),
    %% 可能抛出 channel_termination_timeout 异常，并将 crash 报告写入 sasl 日志中
    send_error_on_channel0_and_close(Channel, Protocol, Reason, State).

send_error_on_channel0_and_close(Channel, Protocol, Reason, State) ->
    {0, CloseMethod} =
        rabbit_binary_generator:map_exception(Channel, Reason, Protocol),
    %% 先关闭当前 connection 下的所有 channel ，再关闭该 connection
    %% terminate_channels/1 可能抛出 channel_termination_timeout 异常
    State1 = close_connection(terminate_channels(State)),
    %% 发送 channel 关闭信令
    ok = send_on_channel0(State#v1.sock, CloseMethod, Protocol),
    State1.

...

format_hard_error(#amqp_error{name = N, explanation = E, method = M}) ->
    io_lib:format("operation ~s caused a connection exception ~s: ~p", [M, N, E]);
format_hard_error(Reason) ->
    case io_lib:deep_char_list(Reason) of
        true  -> Reason;
        false -> rabbit_misc:format("~p", [Reason])
    end.

log_hard_error(#v1{connection_state = CS,
                   connection = #connection{
                                   name  = ConnName,
                                   user  = User,
                                   vhost = VHost}}, Channel, Reason) ->
    log(error,
        "Error on AMQP connection ~p (~s, vhost: '~s',"
        " user: '~s', state: ~p), channel ~p:~n~s~n",
        [self(), ConnName, VHost, User#user.username, CS, Channel, format_hard_error(Reason)]).

...

terminate_channels(#v1{channel_count = 0} = State) ->
    State;
terminate_channels(#v1{channel_count = ChannelCount} = State) ->
    lists:foreach(fun rabbit_channel:shutdown/1, all_channels()),
    %% 根据 channel 数量计算整体关闭时间
    %% 每个 channel 默认预留 3 秒关闭时间
    Timeout = 1000 * ?CHANNEL_TERMINATION_TIMEOUT * ChannelCount,
    TimerRef = erlang:send_after(Timeout, self(), cancel_wait),
    wait_for_channel_termination(ChannelCount, TimerRef, State).

wait_for_channel_termination(0, TimerRef, State) ->
    case erlang:cancel_timer(TimerRef) of
        false -> receive
                     cancel_wait -> State
                 end;
        _     -> State
    end;
wait_for_channel_termination(N, TimerRef,
                             State = #v1{connection_state = CS,
                                         connection = #connection{
                                                         name  = ConnName,
                                                         user  = User,
                                                         vhost = VHost},
                                         sock = Sock}) ->
    receive
        {'DOWN', _MRef, process, ChPid, Reason} ->
            {Channel, State1} = channel_cleanup(ChPid, State),
            case {Channel, termination_kind(Reason)} of
                {undefined,    _} ->
                    exit({abnormal_dependent_exit, ChPid, Reason});
                {_,   controlled} ->
                    wait_for_channel_termination(N-1, TimerRef, State1);
                {_, uncontrolled} ->
                    log(error, "Error on AMQP connection ~p (~s, vhost: '~s',"
                               " user: '~s', state: ~p), channel ~p:"
                               "error while terminating:~n~p~n",
                        [self(), ConnName, VHost, User#user.username,
                         CS, Channel, Reason]),
                    wait_for_channel_termination(N-1, TimerRef, State1)
            end;
        {'EXIT', Sock, _Reason} ->
            [channel_cleanup(ChPid, State) || ChPid <- all_channels()],
            exit(normal);
        cancel_wait ->
            exit(channel_termination_timeout)
    end.
```


----------


### 其他错误

#### operation *Method* caused a channel exception *Name*: *Explanation*

在 log 日志中可以看到如下两种类型的错误

##### precondition_failed 错误

```shell
=ERROR REPORT==== 3-Sep-2016::12:05:44 ===
Channel error on connection <0.15869.1025> (10.0.29.67:22814 -> 10.0.22.20:5672, vhost: 'risk', user: 'risk'), channel 1:
operation queue.declare caused a channel exception precondition_failed: "inequivalent arg 'durable' for queue 'davincimq' in vhost 'risk': received 'true' but current is 'false'"
```

> 错误原因：通过  queue.declare 进行声明时使用的参数和之前使用的参数不一致；

##### not_found 错误

```shell
=ERROR REPORT==== 3-Sep-2016::12:09:06 ===
Channel error on connection <0.6431.1025> (10.0.28.137:48391 -> 10.0.22.20:5672, vhost: 'risk', user: 'risk'), channel 1:
operation queue.declare caused a channel exception not_found: "no queue 'davincimq' in vhost 'risk'"
```

```shell
=ERROR REPORT==== 3-Sep-2016::12:20:27 ===
Channel error on connection <0.26957.1080> (10.0.23.152:34791 -> 10.0.22.20:5672, vhost: 'risk_biz_marketing', user: 'risk_biz_marketing_user'), channel 1:
operation basic.consume caused a channel exception not_found: "no queue 'vangogh_biz_marketing' in vhost 'risk_biz_marketing'"
```

经源码确认，会爆出 not_found 错误的地方共有如下几处：
- 在处理 `queue.declare` 时，若 queue 名字以 "amq.rabbitmq.reply-to" 开头；
- 在处理 `exchange.bind` 或 `exchange.unbind` 或 `queue.bind` 或 `queue.unbind` 时，若执行 binding 的建立或移除时发现对应的资源不存在；
- 在处理 `queue.declare` 时，若 passive 属性设置为 true ，并且发现对应资源不存在；
- 在处理 basic.get 或 queue.purge 或 basic.consume 时，发现资源不存在

因此，可以断定上面的错误原因分别对应 3 和 4 情况；

> ⚠️ 使用被动声明的形式判定 queue 的存在情况，正常情况下页存在这种使用方法；


#### operation none caused a connection exception connection_forced

在日志分析中，发现在一定时间段内大量出现如下错误信息

```shell
=ERROR REPORT==== 3-Sep-2016::12:20:12 ===
Error on AMQP connection <0.22484.5286> (10.0.48.131:26125 -> 10.0.22.20:5672, vhost: 'risk', user: 'risk', state: running), channel 0:
operation none caused a connection exception connection_forced: "broker forced connection closure with reason 'shutdown'"
```

参考日志中的实际情况，统计数值如下：

- 12:20:00 ~ 12:20:11 出现 0 次；
- 12:20:12 出现 4 次；
- 12:20:13 出现 724 次；
- 12:20:14 出现 1314 次；
- 12:20:15 出现 1391 次；
- 12:20:16 出现 1587 次；
- 12:20:17 出现 1766 次；
- 12:20:18 出现 2033 次；
- 12:20:19 出现 2334 次；
- 12:20:20 出现 1823 次；
- 12:20:21 ~ 12:20:59 出现 0 次；

最初在排查该问题时，虽然仅仅是基于错误本身进行的分析，但也得出了一些推测结论，分析过程如下：

在 `rabbit_reader.erl` 中

```erlang
%% 收到来自父进程 rabbit_connection_sup 的 shutdown 退出消息
handle_other({'EXIT', Parent, Reason}, State = #v1{parent = Parent}) ->
    terminate(io_lib:format("broker forced connection closure "
                            "with reason '~w'", [Reason]), State),
    maybe_emit_stats(State),
    exit(Reason);
...
%% 在运行状态中被终止运行（例如收到父进程的退出消息）
terminate(Explanation, State) when ?IS_RUNNING(State) ->
    {normal, handle_exception(State, 0,
                              rabbit_misc:amqp_error(
                                connection_forced, Explanation, [], none))};
terminate(_Explanation, State) ->
    {force, State}.
```

> **connection_forced** 表明连接是被强行终止的；
> **none** 表明发生错误时当前没有进行任何协议处理；

根据 RabbitMQ 的监督树结构，rabbit_reader 进程（对应 AMQP connection）的退出是由于收到来自 rabbit_connection_sup 的退出消息导致；而 rabbit_connection_sup 的退出，或者是由于其上级监督者退出导致（直接针对错误进行分析时，容易忽略这个原因），或者是由于 rabbit_connection_helper_sup 的退出导致（基于 one_for_all 原则）；而 rabbit_connection_helper_sup 的退出的可能原因是其下的 rabbit_queue_collector 和 rabbit_channel_sup_sup 在 10 秒内出现超过 10 次的进程重启导致的；

因此我的最初推论为：该错误应该是由于 channel 进程异常导致的～

然而事实并非如此！

在后续发现统计数值变化的规律后，才确认了导致大量错误发生的根本原因：

- 开始停止 RabbitMQ 服务行为；

```shell
=INFO REPORT==== 3-Sep-2016::12:20:11 ===
Stopping RabbitMQ

=INFO REPORT==== 3-Sep-2016::12:20:12 ===
stopped TCP Listener on [::]:5672
```

- 出现大量错误的时间段；

- 完成停止 RabbitMQ 服务行为；

```shell
=INFO REPORT==== 3-Sep-2016::12:20:20 ===
Stopped RabbitMQ application
```

- 不再出现上述错误信息；

- 重新启动 RabbitMQ 服务；

```shell
=INFO REPORT==== 3-Sep-2016::12:20:24 ===
Starting RabbitMQ 3.6.1 on Erlang R16B03-1
Copyright (C) 2007-2016 Pivotal Software, Inc.
Licensed under the MPL.  See http://www.rabbitmq.com/

=INFO REPORT==== 3-Sep-2016::12:20:24 ===
node           : rabbit@xg-risk-rmq-1
home dir       : /var/lib/rabbitmq
config file(s) : /etc/rabbitmq/rabbitmq.config (not found)
cookie hash    : 6N8Z7edGCIH343OLokZ8iw==
log            : /var/log/rabbitmq/rabbit@xg-risk-rmq-1.log
sasl log       : /var/log/rabbitmq/rabbit@xg-risk-rmq-1-sasl.log
database dir   : /var/lib/rabbitmq/mnesia/rabbit@xg-risk-rmq-1
```

结合这段日志内容，可以断定这次监督树中的进程停止顺序是自上而下触发的！！

值得思考的问题：
- 监督树结构中各个进程的停止顺序是什么？
- 为何会有如此多（共计 12,976 条）的 connection 被强行终止？是否业务不合理的使用了过多的 connection ？


----------

下面的错误信息，在另外的文章中已经分析过了，此处不再赘述；

```shell
=ERROR REPORT==== 3-Sep-2016::12:14:24 ===
Error on AMQP connection <0.13356.1165>: enotconn (socket is not connected)
```

```shell
=ERROR REPORT==== 3-Sep-2016::12:18:39 ===
closing AMQP connection <0.1920.1146> (10.0.252.30:56126 -> 10.0.22.20:5672):
{handshake_timeout,handshake}
```